! -----------------------------------------------------------------------
! ANSI C
!
! The C programming language evolved at Bell Labs from a series of 
! programming languages: 'CPL', 'BCPL', and then 'B'. As a result, C's
! development was a combined effort between Dennis Ritchie, Ken Thompson,
! and Martin Richards.  
!
! C was designed for the creation and implementation of low-level systems
! such as operating systems, device drivers, firmware, etc... To realize 
! this goal, the language contains the ability to perform operations 
! directly on memory and has direct access to system pointers. While this 
! gives an enormous amount of control and flexibility, it also makes C a 
! professional programming language - not to be used by an inexperienced
! programmer.
!
! C (and later C++) quickly became the de facto standard for developing
! operating systems, applications and most other large projects. UNIX as 
! well as Windows, Linux, and Mac-OS X were developed using this 
! language (and its successors).
!
! More information is available at Dennis Ritchie's website:
!     http://cm.bell-labs.com/cm/cs/who/dmr/
!
! The C grammar is inherently ambigious and requires a large number of
! LALR(1) states to parse. As a result, the time required by the GOLD 
! Parser Builder to compile this grammar is extensive.
! 
! C is not a line-based grammar with the notable exception of compiler
! directives (which are preceeded by a '#' character). These are usually not
! handled directly by the actual parser, but, rather, the pre-processor. 
! Before the program is analyzed by the parser, C compilers scan the code and
! act on these commands. The final C program is then passed to the parser.
! -----------------------------------------------------------------------

! -----------------------------------------------------------------------
! This grammar does not contain the compiler directives.
!
! Note: This is an ad hoc version of the language. If there are any flaws, 
! please visit the contact page and tell me.
!
! SPECIAL THANKS TO:
!     BOB MEAGHER
!     MIKE WISDOM
!     VLADIMIR MOROZOV
!     TOM VAN DIJCK
!
! Modified 06/14/2002
!   * The correct definition for "return" was added. Thanks to Bob Meagher.
!   * Added the missing rules for labels, storage specifications, etc...
!     which were left out. Thanks to Mike Wisdom for calling this to
!     my attention.
!
! Modified 06/21/2002
!   * I fixed an error in the grammar for declaring functions. 
!
! Modified 06/15/2003
!   * Vladimir Morozov fixed an error for calling functions with no parameters
!
! Modified 01/31/2004
!   * Tom van Dijck found a bug in the grammar concerning variable 
!     initialization.
!
! Modified 04/26/2004
!   * Some errors in the grammar were fixed.
!
! Modified 01/19/2005
!   * The definition for comments was modified. In ANSI C, block comments
!     cannot be nested. As a result, they are defined using the whitespace
!     terminal.
!
! Modified 03/28/2007
!   * The commented-out definition for non-nested comments was updated. The
!     previous version did not work in all cases.
!
! Modified 03/09/2017 (Kay Gürtzig)
!   * Array / struct initializers introduced
!   * void allowed as parameter list, e.g. int main(void)
!   * 1-d array declarators enabled in parameter lists, as in
!     int main(int argc, char* argv[])
!
! Modified 03/12/2017 (Kay Gürtzig)
!   * const separated from <Mod> such that combinations of const and storage
!     class modifiers are possible in arbitrary order.
!   * <Typedef Decl> rule corrected (was: <Typedef Decl> ::= typedef <Type> ID ';')
!   * block-local type declarations allowed, start symbol changed <Decls> -> <ExtDecls>
!   * Concatenation of at least one string literal
!
! Modified 03/31/2017 (Kay Gürtzig)
!   * floating-point literals and type suffixes for number literals (F, L, LL) introduced
!   * type prefix L for string literals introduced.
!   * user_type_### workaround introduced to allow a preprocessor to circumvent the
!     missing identifier table support in GOLDParser which would be necessary to allow
!     user-defined type names (Id cannot be added to the <Base> alternatives without
!     blowing up the entire grammar due to inherent language ambiguities - C is definitely
!     not context-free!). A preprocessor must detect all type names introduced by typedef
!     and replace them by "user_type_001", "user_type_002" etc. throughout the subsequent
!     lines of the block which are reserved words for user-specific types.
!     On "executing" the obtained parse tree the replacements must of course be reverted.
!
! Modified 04/11/2017 (Kay Gürtzig)
!   * Rules <Struct Decl>, <Union Decl>, and <Enum Decl> enhanced: ';' replaced by <Decl End>
!     in order to allow combined struct/union/enum and variable declarations (was easier here
!     than in <Var Decl>)
!
! Modified 17/06/2018 (Kay Gürtzig)
!   * Rule for return statement without expression had been missing.
!
! Modified 24/06/2018 (Kay Gürtzig)
!   * Operator %= had been missing.
!
! -----------------------------------------------------------------------


"Name"    = 'ANSI-C73' 
"Version" = '1973'
"Author"  = 'Dennis Ritchie, Ken Thompson, Martin Richards' 
"About"   = 'C is one of the most common, and complex, programming languages in use today.'

"Case Sensitive" = True
"Start Symbol"   = <ExtDecls>

{Hex Digit}      = {Digit} + [abcdefABCDEF]
{Oct Digit}      = [01234567]

{Id Head}        = {Letter} + [_] 
{Id Tail}        = {Id Head} + {Digit}

{String Ch}      = {All Printable} - ["]
{Char Ch}        = {All Printable} - ['']

DecLiteral       = [123456789]{digit}*([uU]([lL][lL]?)? | [lL][lL]?[uU]?)?
OctLiteral       = 0{Oct Digit}*([uU]([lL][lL]?)? | [lL][lL]?[uU]?)?
HexLiteral       = 0[xX]{Hex Digit}+([uU]([lL][lL]?)? | [lL][lL]?[uU]?)?
FloatLiteral     = {Digit}*'.'{Digit}+([eE]('+'|'-')?{Digit}+)?[fFlL]?

StringLiteral    = 'L'? ( '"'( {String Ch} | '\'{Printable} )* '"' {Whitespace}* )+
CharLiteral      = '' ( {Char Ch} | '\'{Printable} )''

Id               = {Id Head}{Id Tail}*

UserTypeId       = 'user_type_'{digit}{digit}{digit}


! ===================================================================
! Comments
! ===================================================================

Comment Start = '/*'
Comment End   = '*/'
Comment Line  = '//'


! Typically, C comments cannot be nested. As a result, the 
! Comment Start and Comment End terminals cannot be used.
!
! To implement non-nested comments, the whitespace terminal is
! modified to accept them. In the definition below, Whitespace 
! is defined as one or more {Whitespace} characters OR a series
! of characters delimited by /* and */. Note that the characters
! between the two delimiters cannot contain the */ sequence. 
!
! Uncomment the following to prevent block commments. Make sure 
! to comment the Comment Start and Comment End definitions.
!
! {Non Slash}     = {Printable} - [/]  
! {Non Asterisk}  = {Printable} - [*]
! 
! Whitespace     = {Whitespace}+   
!                | '/*' (  {Non Asterisk} | '*' {Non Slash}? )*  '*/'

!=======================================================

<ExtDecls> ::= <ExtDecl> <ExtDecls>
          |

<ExtDecl>  ::= <Func Decl>
          | <Func Proto>
          | <Decl>

<Decl>  ::= <Struct Decl>
          | <Union Decl>
          | <Enum Decl>
          | <Var Decl>    
          | <Typedef Decl>
  
! ===================================================================
! Function  Declaration
! ===================================================================

<Func Proto> ::= <Func ID> '(' <Types>  ')' ';'
               | <Func ID> '(' <Params> ')' ';'
               | <Func ID> '(' void ')' ';'
               | <Func ID> '(' ')' ';'

<Func Decl>  ::= <Func ID> '(' <Params>  ')' <Block>
               | <Func ID> '(' <Id List> ')' <Struct Def> <Block>
               | <Func ID> '(' void ')' <Block>
               | <Func ID> '(' ')' <Block>


<Params>     ::= <Param> ',' <Params>
               | <Param>
               
<Param>      ::= <ConstType> ID <Array>

<Types>      ::= <ConstType>  ',' <Types>
               | <ConstType> 
   
<Id List>    ::= Id ',' <Id List>
               | Id

<Func ID>    ::= <ConstMod> <Type> ID
               | <Mod> const <Type> ID
               | <ConstType> ID
               | void ID
               | <Mod> void ID
               | ID

! ===================================================================
! Type Declaration
! ===================================================================

<Typedef Decl> ::= typedef <Var Decl>

<Struct Decl>  ::= struct Id '{' <Struct Def> '}' <Decl End>

<Union Decl>   ::= union Id '{' <Struct Def> '}' <Decl End> 


<Struct Def>   ::= <Var Decl> <Struct Def>
                 | <Var Decl>
 
<Decl End> ::= ';'
             | <Var Item> <Var List> ';'

! ===================================================================
! Variable Declaration
! ===================================================================

<Var Decl>     ::= <ConstMod> <Type>  <Var> <Var List>  ';'
                 | <Mod> const <Type> <Var> <Var List>  ';'
                 |       <ConstType>  <Var> <Var List>  ';'
                 | <ConstMod>         <Var> <Var List>  ';'
                 | const              <Var> <Var List>  ';'
             
<Var>      ::= ID <Array>
             | ID <Array> '=' <Initializer> 

<Array>    ::= '[' <Expr> ']'
             | '[' ']'
             |
             
<Var List> ::=  ',' <Var Item> <Var List>
             | 

<Var Item> ::= <Pointers> <Var>

<ConstMod> ::= const <Mod>
             | <Mod>
 
<Mod>      ::= extern 
             | static
             | register
             | auto
             | volatile

! ===================================================================
! Enumerations
! ===================================================================

<Enum Decl>    ::= enum Id '{' <Enum Def> '}' <Decl End>
 
<Enum Def>     ::= <Enum Val> ',' <Enum Def>
                 | <Enum Val>

<Enum Val>     ::= Id
                 | Id '=' OctLiteral
                 | Id '=' HexLiteral
                 | Id '=' DecLiteral  


! ===================================================================
! Types
! ===================================================================

<ConstType> ::= const <Type>
              | <Type>
              
<Type>     ::= <Base> <Pointers>

<Base>     ::= <Sign> <Scalar>
             | struct Id 
             | struct '{' <Struct Def> '}' 
             | union Id
             | union '{' <Struct Def> '}' 
             | enum Id
             | enum '{' <Enum Def> '}' 
             | void '*'
             | UserTypeId       ! workaround for the impossibility to place Id here

<Sign>     ::= signed 
             | unsigned
             |

<Scalar>   ::= char
             | wchar_t
             | int
             | short
             | long
             | short int
             | long int
             | long long
             | long long int
             | float
             | double
 
<Pointers> ::= '*' <Pointers>
             | '*' const <ConstPointers>
             |

<ConstPointers> ::= '*' const <ConstPointers>
             | '*'
             |

! ===================================================================
! Statements
! ===================================================================

<Stm>        ::= Id ':'                            !Label
               | if '(' <Expr> ')' <Stm>          
               | if '(' <Expr> ')' <Then Stm> else <Stm>         
               | while '(' <Expr> ')' <Stm> 
               | for '(' <Arg> ';' <Arg> ';' <Arg> ')' <Stm>
               | <Normal Stm>

<Then Stm>   ::= if '(' <Expr> ')' <Then Stm> else <Then Stm> 
               | while '(' <Expr> ')' <Then Stm> 
               | for '(' <Arg> ';' <Arg> ';' <Arg> ')' <Then Stm>
               | <Normal Stm>

<Normal Stm> ::= do <Stm> while '(' <Expr> ')'
               | switch '(' <Expr> ')' '{' <Case Stms> '}'
               | <Block>
               | <Expr> ';'               
               | goto Id ';'
               | break ';'
               | continue ';'
               | return <Expr> ';'
               | return ';'
               | ';'              !Null statement


<Arg>       ::= <Expr> 
              | 

<Case Stms> ::= case <Value> ':' <Stm List> <Case Stms>
              | default ':' <Stm List>                  
              |

<Block>     ::= '{' <Decl Stm List> '}' 
 
<Decl Stm List> ::= <Decl> <Decl Stm List>
              | <Stm List>

<Stm List>  ::=  <Stm> <Stm List> 
              | 


! ===================================================================
! Here begins the C's 15 levels of operator precedence.
! ===================================================================

<Initializer> ::= <Op If>
                | '{' <ExprIni> '}' 

<Expr>       ::= <Expr> ',' <Op Assign>   
               | <Op Assign>
   
<ExprIni>    ::= <ExprIni> ',' <Initializer>
               | <Initializer>
   

<Op Assign>  ::= <Op If> '='   <Op Assign>
               | <Op If> '+='  <Op Assign>
               | <Op If> '-='  <Op Assign>
               | <Op If> '*='  <Op Assign>
               | <Op If> '/='  <Op Assign>
               | <Op If> '%='  <Op Assign>
               | <Op If> '^='  <Op Assign>
               | <Op If> '&='  <Op Assign>
               | <Op If> '|='  <Op Assign>
               | <Op If> '>>=' <Op Assign>
               | <Op If> '<<=' <Op Assign>
               | <Op If>

<Op If>      ::= <Op Or> '?' <Op If> ':' <Op If>
               | <Op Or>

<Op Or>      ::= <Op Or> '||' <Op And>
               | <Op And>

<Op And>     ::= <Op And> '&&' <Op BinOR>
               | <Op BinOR>

<Op BinOR>   ::= <Op BinOr> '|' <Op BinXOR>
               | <Op BinXOR>

<Op BinXOR>  ::= <Op BinXOR> '^' <Op BinAND>
               | <Op BinAND>

<Op BinAND>  ::= <Op BinAND> '&' <Op Equate>
               | <Op Equate>

<Op Equate>  ::= <Op Equate> '==' <Op Compare>
               | <Op Equate> '!=' <Op Compare>
               | <Op Compare>

<Op Compare> ::= <Op Compare> '<'  <Op Shift>
               | <Op Compare> '>'  <Op Shift>
               | <Op Compare> '<=' <Op Shift>
               | <Op Compare> '>=' <Op Shift>
               | <Op Shift>

<Op Shift>   ::= <Op Shift> '<<' <Op Add>
               | <Op Shift> '>>' <Op Add>
               | <Op Add>

<Op Add>     ::= <Op Add> '+' <Op Mult>
               | <Op Add> '-' <Op Mult>
               | <Op Mult>

<Op Mult>    ::= <Op Mult> '*' <Op Unary>
               | <Op Mult> '/' <Op Unary>
               | <Op Mult> '%' <Op Unary>
               | <Op Unary>

<Op Unary>   ::= '!'    <Op Unary>
               | '~'    <Op Unary>   
               | '-'    <Op Unary>
               | '*'    <Op Unary>
               | '&'    <Op Unary>               
               | '++'   <Op Unary>
               | '--'   <Op Unary>
               | <Op Pointer> '++'
               | <Op Pointer> '--'
               | '(' <ConstType> ')' <Op Unary>   !CAST
               | sizeof '(' <ConstType> ')'
!               | sizeof '(' ID <Pointers> ')'  !  Awkward attempt to allow type ids here which aren't elsewhere
               | sizeof '(' <Pointers> <Op Pointer> ')'
               | <Op Pointer>

<Op Pointer> ::= <Op Pointer> '.' <Call Id>
               | <Op Pointer> '->' <Call Id>
               | <Op Pointer> '[' <Expr> ']'
               | <Value>

<Call Id>    ::= Id '(' <Expr> ')'
               | Id '(' ')'          
               | Id
   
<Value>      ::= OctLiteral
               | HexLiteral
               | DecLiteral  
               | StringLiteral
               | CharLiteral
               | FloatLiteral
               | <Call Id>
               | '(' <Expr> ')'
